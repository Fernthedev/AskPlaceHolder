import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '6.1.0'
}

group = 'com.github.fernthedev'
version = '1.0.0'

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

repositories {
    mavenCentral()
    maven { url = "https://repo.aikar.co/content/groups/aikar/" } // Required to solve transistive dependency issues
    maven { url 'https://jitpack.io' }
    maven {
        name = 'spigotmc-repo'
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
    maven {
        name = 'placeholderapi'
        url = 'http://repo.extendedclip.com/content/repositories/placeholderapi/'
    }
}

dependencies {
    compileOnly 'org.spigotmc:spigot-api:1.16.4-R0.1-SNAPSHOT'
    implementation 'com.github.Fernthedev.FernAPI:spigot:60b78dc'
    compileOnly group: 'me.clip', name: 'placeholderapi', version: '2.10.9'
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        filter ReplaceTokens, tokens: [version: version]
    }
}


jar.dependsOn shadowJar
shadowJar {
    minimize()
    relocate 'fr.minuskube.inv', "${project.group}.smartinv"
    relocate 'com.github.fernthedev', "${project.group}.fernstuff"
    relocate 'co.aikar.commands', "${project.group}.acf"
    relocate 'co.aikar.locales', "${project.group}.locales"

}

compileJava {
    options.compilerArgs += ["-parameters"]
    options.fork = true
    options.forkOptions.executable = 'javac'
}
